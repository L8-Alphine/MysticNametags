plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'com.mystichorizons.mysticnametags'
version = '1.1.5'

repositories {
    mavenCentral()
    maven {
        url "https://repo.codemc.io/repository/creatorfromhell/"
    }
}

dependencies {
    compileOnly files('libs/HytaleServer.jar')

    // JSR305 annotations (@Nonnull, @Nullable)
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.24'

    // MySQL Connector
    implementation 'com.mysql:mysql-connector-j:9.3.0'

    // SQLite JDBC Driver
    implementation 'org.xerial:sqlite-jdbc:3.45.1.0'

    // PlaceholderAPI
    compileOnly files('libs/WiFlowPlaceholderAPI-1.0.0.jar')
    compileOnly files('libs/PlaceholderAPI-1.0.3-CurseForge.jar')

    // VaultUnlocked Support
    compileOnly 'net.cfh.vault:VaultUnlocked:2.18.3'

    // EliteEssentials Support
    compileOnly files('libs/EliteEssentials-1.1.6.jar')

    // TheEconomy Support
    compileOnly files('libs/EconomySystem-1.0.8.1-beta.jar')

    // HyEssentialsX Support
    compileOnly files('libs/hyessentialsx-1.2.9.jar')

    // EcoTale Support
    compileOnly files('libs/Ecotale-1.0.7.jar')

    // CoinsAndMarkets Support
    compileOnly files('libs/CoinsAndMarkets-0.2.6.jar')

    // Luckperms Support
    compileOnly 'net.luckperms:api:5.4'

    // PermissionsPlus
    compileOnly files('libs/PermissionsPlus-1.1.0.jar')

    // PrefixesPlus
    compileOnly files('libs/PrefixesPlus-3.2.6.jar')

    //RPGLeveling Support
    compileOnly files('libs/RPGLeveling-0.2.2.jar')

    // Endless Leveling
    compileOnly files('libs/EndlessLeveling.jar')

    // TEMP
    compileOnly files('libs/EcoTaleQuests-1.3.3.jar')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

shadowJar {
    archiveClassifier.set('')
    // Exclude server classes from the final JAR
    dependencies {
        exclude(dependency { it.moduleGroup == 'com.hypixel' })
    }
}

// Disable the default jar task to avoid conflicts with shadowJar
tasks.named('jar') {
    enabled = false
}

tasks.named('build') {
    dependsOn shadowJar
}

// Task to copy server JAR to libs folder if not present
tasks.register('copyServerJar') {
    doLast {
        def destJar = file('libs/HytaleServer.jar')
        if (!destJar.exists()) {
            def sources = [file('server/HytaleServer.jar'), file('../server/HytaleServer.jar')]
            for (src in sources) {
                if (src.exists()) {
                    copy {
                        from src
                        into 'libs'
                    }
                    break
                }
            }
        }
    }
}

tasks.named('compileJava') {
    dependsOn 'copyServerJar'
}

// Deploy plugin JAR to server mods folder
tasks.register('deployToServer', Copy) {
    // Using 'from shadowJar' automatically adds task dependency and proper input tracking
    from shadowJar
    into 'server/mods'
    doLast {
        println "Deployed to server/mods/"
    }
}

// Watch for changes and auto-rebuild (useful during development)
tasks.register('watch') {
    doLast {
        println "Watching for changes... Press Ctrl+C to stop."
        println "Run 'gradle build --continuous' for auto-rebuild on file changes."
    }
}